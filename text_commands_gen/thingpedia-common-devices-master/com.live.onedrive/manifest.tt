class @com.live.onedrive {
  import loader from @org.thingpedia.v1();
  import config from @org.thingpedia.config.oauth2();

  monitorable list query list_files(out file_id: Entity(com.live.onedrive:file_id),
                                    out file_name: Entity(tt:path_name),
                                    out description: String,
                                    out created_time: Date,
                                    out modified_time: Date,
                                    out modified_by: Entity(com.live.onedrive:user_id),
                                    out link: Entity(tt:url),
                                    in opt order_by: Enum(created_time_increasing,created_time_decreasing,modified_time_increasing,modified_time_decreasing,name_increasing,name_decreasing))
  #_[canonical="file created on onedrive"]
  #_[confirmation="files your OneDrive"]
  #_[confirmation_remote="files on $__person's OneDrive"]
  #_[formatted=[{type="text",text="New file created: ${file_name}"}]]
  #[poll_interval=3600000ms]
  #[doc="list files in your onedrive"];

  query open(in req file_name: Entity(tt:path_name) #_[prompt="What file do you want to open? Use the full path name."],
             out url: Entity(tt:url))
  #_[canonical="open on onedrive"]
  #_[confirmation="the link to $file_name in your Onedrive"]
  #_[confirmation_remote="the link to $file_name in $__person's Onedrive"]
  #_[formatted=[{type="rdl",webCallback="${url}",displayTitle="${file_name}"}]]
  #[doc="open (retrieve a temporary link to) a file"];

  action create_file(in req file_name: Entity(tt:path_name) #_[prompt="What should be the name of the file?"],
                     in req contents: String #_[prompt="What do you want to write in the file?"])
  #_[canonical="create file on onedrive"]
  #_[confirmation="create a new file on OneDrive named $file_name containing $contents"]
  #[doc="create a new file in your onedrive"];

  action delete_file(in req file_name: Entity(tt:path_name) #_[prompt="What file do you want to delete?"])
  #_[canonical="delete file on onedrive"]
  #_[confirmation="delete the file $file_name from OneDrive"]
  #[doc="delete a file in your onedrive"];

  action rename_file(in req old_name: Entity(tt:path_name) #_[prompt="What file do you want to rename?"],
                     in req new_name: Entity(tt:path_name) #_[prompt="What should be the new name?"])
  #_[canonical="rename file on onedrive"]
  #_[confirmation="rename $old_name to $new_name on OneDrive"]
  #[doc="rename a file in your onedrive"];

  action upload_picture(in req file_name: Entity(tt:path_name) #_[prompt="What should be the file name of the picture?"],
                        in req picture_url: Entity(tt:picture) #_[prompt="Upload the picture now."])
  #_[canonical="upload picture on onedrive"]
  #_[confirmation="upload a picture on OneDrive as $file_name"]
  #[doc="upload a picture in your onedrive"];
}

