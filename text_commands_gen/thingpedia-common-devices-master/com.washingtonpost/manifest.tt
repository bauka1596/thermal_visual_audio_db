class @com.washingtonpost {
  import loader from @org.thingpedia.v2();
  import config from @org.thingpedia.config.none();

  monitorable list query get_article(in req section: Enum(politics,opinions,local,sports,national,world,business,lifestyle) #_[prompt="What section do you want to read?"],
                                     out title: String #[string_values="tt:news_title"],
                                     out link: Entity(tt:url),
                                     out description: String #[string_values="tt:news_description"],
                                     out picture_url: Entity(tt:picture))
  #_[canonical="articles on washington post"]
  #_[confirmation="the latest articles in the ${section} section of the Washington Post"]
  #_[formatted=[{type="rdl",webCallback="${link}",displayTitle="${title}",displayText="${description}"}, {type="picture",url="${picture_url}"}]]
  #[poll_interval=10800000ms]
  #[doc="read washington post"];

  monitorable list query get_blog_post(in req section: Enum(the_fix,politics,powerpost,fact_checker,world_views,compost,the_plum_line,post_partisan,post_everything,right_turn,capital_weather_gang,morning_mix,wonkblog) #_[prompt="What blog do you want to monitor?"],
                                       out title: String #[string_values="tt:news_title"],
                                       out link: Entity(tt:url),
                                       out updated: Date)
  #_[canonical="blog post on washington post"]
  #_[confirmation="the latest articles in the $section blog of the Washington Post"]
  #_[formatted=[{type="rdl",webCallback="${link}",displayTitle="${title}"}]]
  #[poll_interval=10800000ms]
  #[doc="trigger on new blog posts"];
}
