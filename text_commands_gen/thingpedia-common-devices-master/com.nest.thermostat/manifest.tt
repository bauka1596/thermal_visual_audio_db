class @com.nest.thermostat extends @thermostat {
  import loader from @org.thingpedia.embedded();
  import config from @org.thingpedia.config.builtin();

  monitorable query get_humidity(out value: Number)
  #_[canonical="get humidity on thermostat"]
  #_[confirmation="the humidity reported by Nest Thermostat"]
  #_[confirmation_remote="the humidity reported by $__person's thermostat"]
  #_[formatted=[{type="text",text="Humidity: ${value}%"}]]
  #[poll_interval=0ms]
  #[doc="get the current humidity (in percent)"];

  monitorable query get_hvac_state(out mode: Enum(heat,cool,heat_cool,off),
                                   out state: Enum(heating,cooling,off))
  #_[canonical="get hvac state on thermostat"]
  #_[confirmation="the state of your Nest Thermostat"]
  #_[confirmation_remote="the state of $__person's Nest Thermostat"]
  #_[formatted=[{type="text",text="Your thermostat is ${state}."}]]
  #[poll_interval=0ms]
  #[doc="get the state of the heating system"];

  monitorable query get_temperature(out value: Measure(C))
  #_[canonical="get temperature on thermostat"]
  #_[confirmation="the temperature on your thermostat"]
  #_[confirmation_remote="the temperature on $__person's thermostat"]
  #_[formatted=[{type="text",text="Ambient temperature: ${value:C}"}]]
  #[poll_interval=0ms]
  #[doc="get the current temperature"];

  action set_hvac_mode(in req mode: Enum(heat,cool,heat_cool,off) #_[prompt="What mode do you want to set?"])
  #_[canonical="set hvac mode on thermostat"]
  #_[confirmation="set your Nest Thermostat to $mode"]
  #_[confirmation_remote="set your Nest Thermostat to $mode"]
  #[doc="change the mode of the HVAC system associated with this thermostat"];

  action set_minmax_temperature(in req low: Measure(C) #_[prompt="What low temperature do you want to set?"],
                                in req high: Measure(C) #_[prompt="What high temperature do you want to set?"])
  #_[canonical="set minimum maximum temperature on thermostat"]
  #_[confirmation="set your Nest Thermostat between $low and $high"]
  #_[confirmation_remote="set your Nest Thermostat between $low and $high"]
  #[doc="set desired temperature range"];

  action set_target_temperature(in req value: Measure(C) #_[prompt="What temperature do you want to set?"])
  #_[canonical="set target temperature on thermostat"]
  #_[confirmation="set your Nest Thermostat to $value"]
  #_[confirmation_remote="set your Nest Thermostat to $value"]
  #[doc="set target temperature"];
}
