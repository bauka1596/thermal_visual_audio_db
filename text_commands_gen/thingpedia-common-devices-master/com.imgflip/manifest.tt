class @com.imgflip {
  import loader from @org.thingpedia.v2();
  import config from @org.thingpedia.config.none();

  list query list(out name: String #_[prompt="What meme do you want?"],
                  out picture_url: Entity(tt:picture))
  #_[canonical="list meme templates on imgflip"]
  #_[confirmation="meme templates available on Imgflip"]
  #_[formatted=[{type="text",text="${name}"}, {type="picture",url="${picture_url}"}]]
  #[doc="list available meme templates"];

  query generate(in req template: Entity(imgflip:meme_id) #_[prompt="What meme do you want to generate?"],
                 in req text_top: String #_[prompt="What do you want to write at the top? Leave blank for the default (if recognized)"] #[string_values="com.imgflip:meme_text"],
                 in req text_bottom: String #_[prompt="What do you want to write at the bottom? Leave blank for the default (if recognized)"] #[string_values="com.imgflip:meme_text"],
                 out picture_url: Entity(tt:picture))
  #_[canonical="generate meme on imgflip"]
  #_[confirmation="the meme $template with $text_top at the top and $text_bottom at the bottom, generated using Imflip"]
  #_[formatted=[{type="picture",url="${picture_url}"}]]
  #[doc="generate a meme by name"];
}
