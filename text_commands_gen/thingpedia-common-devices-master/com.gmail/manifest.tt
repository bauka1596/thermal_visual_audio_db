class @com.gmail {
  import loader from @org.thingpedia.v2();
  import config from @org.thingpedia.config.oauth2(client_id=$undefined, client_secret=$undefined);

  monitorable list query inbox(out sender_name: String #[string_values="tt:person_full_name"],
                               out sender_address: Entity(tt:email_address),
                               out subject: String #[string_values="tt:email_subject"],
                               out date: Date,
                               out labels: Array(String),
                               out snippet: String #[string_values="tt:long_free_text"],
                               out thread_id: Entity(com.gmail:thread_id),
                               out email_id: Entity(com.gmail:email_id),
                               in opt is_important: Boolean,
                               in opt is_primary: Boolean)
  #_[canonical="list email in inbox"]
  #_[confirmation="the emails in your GMail inbox"]
  #_[confirmation_remote="the emails in $__person's GMail inbox"]
  #_[formatted=[{type="text",text="From ${sender_name} <${sender_address}>: ${subject}"}, {type="text",text="${snippet}"}]]
  #[poll_interval=3600000ms]
  #[doc="retrieve your inbox"];

  action send_email(in req to: Entity(tt:email_address) #_[prompt="Who do you want to send to?"],
                    in req subject: String #_[prompt="With what subject?"] #[string_values="tt:email_subject"],
                    in req message: String #_[prompt="What message do you want to send?"] #[string_values="tt:long_free_text"])
  #_[canonical="send email on gmail"]
  #_[confirmation="send an email to $to with subject $subject on GMail"]
  #_[confirmation_remote="send an email to $to with subject $subject using $__person's GMail"]
  #[doc="send an email"];

  action send_picture(in req to: Entity(tt:email_address) #_[prompt="Who do you want to send to?"],
                      in req subject: String #_[prompt="With what subject?"] #[string_values="tt:email_subject"],
                      in req message: String #_[prompt="What message do you want to send?"] #[string_values="tt:long_free_text"],
                      in req picture_url: Entity(tt:picture) #_[prompt="Upload the picture now."])
  #_[canonical="send picture on gmail"]
  #_[confirmation="send a picture to $to with subject $subject on GMail"]
  #_[confirmation_remote="send a picture to $to with subject $subject using $__person's GMail"]
  #[doc="send an email with a picture attached"];

  action reply(in req email_id: Entity(com.gmail:email_id) #_[prompt="What email do you want to reply to?"],
               in opt subject: String #[string_values="tt:email_subject"],
               in req message: String #_[prompt="What do you want to say in your reply?"] #[string_values="tt:long_free_text"])
  #_[canonical="gmail reply email"]
  #_[confirmation="reply to $email_id saying $message"]
  #_[confirmation_remote="reply to $email_id saying $message from $__person's GMail"]
  #[doc="reply to emails"];

  action forward(in req email_id: Entity(com.gmail:email_id) #_[prompt="What email do you want to forward?"],
                 in req to: Entity(tt:email_address) #_[prompt="Who do you want to forward it to?"],
                 in req message: String #_[prompt="What do you want to say in the email?"] #[string_values="tt:long_free_text"])
  #_[canonical="gmail forward email"]
  #_[confirmation="forward $email_id to $to saying $message with your GMail"]
  #_[confirmation_remote="forward $email_id to $to saying $message with $__person's GMail"]
  #[doc="forward an email"];
}
