class @me.omlet.chat {
  import loader from @org.thingpedia.v1();
  import config from @org.thingpedia.config.oauth2();

  monitorable list query messages(out feed: Entity(omlet:feed_id) #_[prompt="What feed do you want to monitor?"],
                                  out message: String,
                                  in opt from_me: Boolean)
  #_[canonical="list messages on omlet"]
  #_[confirmation="Omlet messages"]
  #_[confirmation_remote="$__person's Omlet messages"]
  #_[formatted=[{type="text",text="Message in ${feed}: ${message}"}]]
  #[poll_interval=0ms]
  #[doc="list omlet messages in a feed (or in all feeds)"];

  action send_to(in req to: Entity(tt:username) #_[prompt="Who do you want to send the message to? Use the Omlet ID, phone number or email address."],
                 in req message: String #_[prompt="What do you want to say?"])
  #_[canonical="send message on omlet"]
  #_[confirmation="send a message on Omlet to $to saying $message"]
  #[doc="send a message to a specific identity"];

  action send_picture(in req to: Entity(tt:username) #_[prompt="Who do you want to send the message to? Use the Omlet ID, phone number or email address."],
                      in req picture_url: Entity(tt:picture) #_[prompt="Upload the picture you want to send now."],
                      in req caption: String #_[prompt="What caption should the picture have?"])
  #_[canonical="send picture on omlet"]
  #_[confirmation="send a picture on Omlet to $to with caption $caption"]
  #[doc="send a picture to a specific identity"];
}

